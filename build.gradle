def teamDir = new File(buildscript.sourceFile.parentFile, 'team-props')
ext.checkstyleConfigDir = "$teamDir"

buildscript {
    repositories {
        maven {
            url 'http://repo1.maven.org/maven2'
        }
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.6.+'
        classpath 'com.squareup.gradle:gradle-android-test-plugin:0.9.1-SNAPSHOT'
    }
}

def isReleaseBuild() {
    return hasProperty("release")
}

def isCiBuild() {
    return hasProperty("ci")
}

def isDevBuild() {
    return !isReleaseBuild() && !isCiBuild()
}

allprojects {
    version = '1.6.1'
    repositories {
        mavenCentral()
        maven {
            url "http://www.bugsense.com/gradle/"
        }
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
        maven {
            url "https://github.com/novoda/public-mvn-repo/raw/master/releases"
        }
    }

    apply plugin: 'checkstyle'
    checkstyle {
        ignoreFailures = true
        configFile = new File(teamDir, "checkstyle.xml")
        configProperties.checkstyleConfigDir = checkstyleConfigDir
    }

    apply plugin: 'findbugs'
    findbugs {
        ignoreFailures = true
    }

    apply plugin: 'pmd'
    pmd {
        ignoreFailures = true
    }
}

apply plugin: 'maven'
apply plugin: 'signing'

version = "1.6.1-SNAPSHOT"

def isReleaseBuild() {
    !version.contains("SNAPSHOT")
}

def sonatypeRepositoryUrl

if (isReleaseBuild()) {
    println "RELEASE BUILD $version"
    sonatypeRepositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
} else {
    println "DEBUG BUILD $version"
    sonatypeRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
}

def username = project.hasProperty("nexusUsername") ? project.property("nexusUsername") : ""
def password = project.hasProperty("nexusPassword") ? project.property("nexusPassword") : ""

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { deployment -> signing.signPom(deployment) }

                repository(url: sonatypeRepositoryUrl) {
                    authentication(userName: username, password: password)
                }

                pom.project {
                    name 'imageloader-core'
                    groupId 'com.novoda.imageloader'
                    description 'ImageLoader for Android'
                    url 'http://github.com/novoda/ImageLoader'

                    scm {
                        url 'scm:git@github.com:novoda/ImageLoader.git'
                        connection 'scm:git@github.com:novoda/ImageLoader.git'
                        developerConnection 'scm:git@github.com:novoda/ImageLoader.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'luigiagosti'
                            name 'Luigi Agosti'
                        }
                        developer {
                            id 'charroch'
                            name 'Carl-Gustaf Harroch'
                        }
                        developer {
                            id 'frankiesardo'
                            name 'Frankie Sardo'
                        }
                        developer {
                            id 'ataulm'
                            name 'Ataul Munim'
                        }
                    }
                }
            }
        }
    }

    signing {
        required { isReleaseBuild() && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    task androidJavadocs(type: Javadoc) {
        source = android.sourceSets.main.allJava
    }

    task androidJavadocsJar(type: Jar) {
        classifier = 'javadoc'
        //basename = artifact_id
        from androidJavadocs.destinationDir
    }

    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        //basename = artifact_id
        from android.sourceSets.main.allSource
    }

    artifacts {
        archives packageReleaseJar
        archives androidSourcesJar
        archives androidJavadocsJar
    }
}